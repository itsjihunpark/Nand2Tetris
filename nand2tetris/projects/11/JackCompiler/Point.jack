/** Represent a two-dimensional point
     File name: Point.jack */
class Point{
     
     //The coordinatees of this point:
     field int x, y;

     //The number of Point object constructed so far:
     static int pointCount;

     /** Constructs a two dimensional poitn and
          initializes it with the given coordinates. */
     constructor Point new(int ax, int ay) {
          let x = ax;
          let y = ay;
          let pointCount = pointCount + 1;
          return this;
     }
     /** Returns the x coordinate of this point. */
     method int getx(){ return x; }

     /** Returns the y coordinate of this point. */
     method int gety(){ return y; }

     /** Returns the number of Point constructed so far. */
     function int getPointCount(){
          return pointCount;
     }
     
     /** Returns a point which is this point plus
          the other point. */
     method Point plus(Point other){
          return Point.new(x + other.getx(),
                           y + other.gety());
     }

     /** Returns the Euclidean distance between
          this and the other point. */
     method int distance(Point other){
          var int dx, dy;
          let dx = x - other.getx();
          let dy = y - other.gety();
          return Math.sqrt((dx*dx)+(dy*dy));
     }

     /** Prints this point as "(x,y)"*/
     method void print(){
          
          do Output.printString("(");
          do Output.printInt(x);
          do Output.printInt(",");
          do Output.printInt(y);
          do Output.printString(")");
          return;
     }
} // End of Point class declaration